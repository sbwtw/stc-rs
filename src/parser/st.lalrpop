use crate::ast::*;
use crate::parser::lexer::{self, LiteralType};

grammar;

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok {
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "*" => lexer::Tok::Multiply,
        "/" => lexer::Tok::Division,
        "(" => lexer::Tok::LeftParentheses,
        ")" => lexer::Tok::RightParentheses,
        "," => lexer::Tok::Comma,
        ";" => lexer::Tok::Semicolon,
        "Literal" => lexer::Tok::Literal(<LiteralType>),
    }
}

//Comma<T>: Vec<T> = {
//    <v:(<T> ",")*> <e:T?> => match e {
//        None => v,
//        Some(e) => {
//            let mut v = v;
//            v.push(e);
//            v
//        }
//    }
//};

pub CompilationUnits: StatementList = {
    Statement* => StatementList(<>)
}

Statement: Box<dyn Statement> = {
    <e:Expr> ";" => Box::new(ExprStatement::new(e))
}

Expr: Box<dyn Expression> = {
    <e1: Expr> <op: ExprOp> <e2: Factor> => Box::new(OperatorExpression::new(op, vec![e1, e2])),
    Factor,
};

ExprOp: OpCode = {
    "+" => OpCode::Add,
    "-" => OpCode::Sub,
};

Factor: Box<dyn Expression> = {
    <e1: Factor> <op: FactorOp> <e2: UnaryFactor> => Box::new(OperatorExpression::new(op, vec![e1, e2])),
    UnaryFactor,
};

FactorOp: OpCode = {
    "*" => OpCode::Mul,
    "/" => OpCode::Div,
};

UnaryFactor: Box<dyn Expression> = {
    <op: UnaryOp> <e: Term> => Box::new(OperatorExpression::new(op, vec![e])),
    Term,
}

UnaryOp: OpCode = {
    "-" => OpCode::Sub,
};

Term: Box<dyn Expression> = {
    "Literal" => Box::new(LiteralExpression::new(<>)),
    "(" <Expr> ")"
};
